#pushdefine

#pushdefine
#include "\Lib\Parts\RES\HistoryDataEditDialog_res" "..\Lib\Parts\RES\HistoryDataEditDialog_res"
#popdefine

Package HistoryDataEditDialog

    #define DEF_FIELD_NAME_TEXT_PROPERTY 0
    #define DEF_PROPERTY_NUM 1
    _properties[DEF_PROPERTY_NUM]


    _logEditType 

    _editValue = ""

    DialogResult

    Method SetFieldNameText(value) /*As String*/
    Begin
        _properties[DEF_FIELD_NAME_TEXT_PROPERTY] = value
        BtLabelCommon:SetText(HistoryDataEditDialog_labelFieldName:Properties, "[" & value & "]")
    End Method



    Method New()
    Begin
        DialogResult = DIALOG_RESULT_CANCEL
        HistoryDataEditDialog_res:InitializeComponent()

        With BtTextBoxCommon
            :SetWidth(HistoryDataEditDialog_textBox:Properties, BtInputDateBoxCommon:GetWidth(HistoryDataEditDialog_inputDateBox:Properties))
            :SetHeight(HistoryDataEditDialog_textBox:Properties, BtInputDateBoxCommon:GetHeight(HistoryDataEditDialog_inputDateBox:Properties))
            :SetTop(HistoryDataEditDialog_textBox:Properties, BtInputDateBoxCommon:GetTop(HistoryDataEditDialog_inputDateBox:Properties))
            :SetLeft(HistoryDataEditDialog_textBox:Properties, BtInputDateBoxCommon:GetLeft(HistoryDataEditDialog_inputDateBox:Properties))
        End With

        HistoryDataEditDialog_cancelButton:Invalidate()
        HistoryDataEditDialog_okButton:Invalidate()
    End Method

    Method CreateTextEditArea(value)
    Begin
        ShowTextBox(value, INPUT_CONTROL_STRING_TEXT_BOX, 0)
        _logEditType = LOG_EDIT_TYPE_TEXT_BOX
        HistoryDataEditDialog_FocusTransitionUtility:Reset()
        HistoryDataEditDialog_FocusTransitionUtility:SetControl("HistoryDataEditDialog_textBox")
        HistoryDataEditDialog_textBox:Focus()

        HistoryDataEditDialog_FocusTransitionUtility:SetControl("HistoryDataEditDialog_cancelButton")
        HistoryDataEditDialog_FocusTransitionUtility:SetControl("HistoryDataEditDialog_okButton")
    End Method

    Method CreateEditArea(value, screenData)
    Begin
        HistoryDataEditDialog_FocusTransitionUtility:Reset()
        Select Case ILogItemScreenData:GetLogEditType(screenData)
            Case
                INPUT_CONTROL_STRING_TEXT_BOX,
                INPUT_CONTROL_REAL_TEXT_BOX,
                INPUT_CONTROL_INTEGER_TEXT_BOX,
                INPUT_CONTROL_RADIO_BUTTON,
                INPUT_CONTROL_CHECK_BOX
                ShowTextBox(
                    value,
                    ILogItemScreenData:GetLogEditType(screenData),
                    ILogItemScreenData:GetDecimalPlaces(screenData))
                _logEditType = LOG_EDIT_TYPE_TEXT_BOX

                HistoryDataEditDialog_FocusTransitionUtility:SetControl("HistoryDataEditDialog_textBox")
                HistoryDataEditDialog_textBox:Focus()
            Case INPUT_CONTROL_DATE_BOX
                ShowDateBox(
                    value,
                    ILogItemScreenData:GetDateFormatType(screenData),
                    ILogItemScreenData:GetDateSeparatorType(screenData))
                _logEditType = LOG_EDIT_TYPE_DATE_BOX

                HistoryDataEditDialog_FocusTransitionUtility:SetControl("HistoryDataEditDialog_inputDateBox")
                HistoryDataEditDialog_inputDateBox:Focus()
        End Select

        HistoryDataEditDialog_FocusTransitionUtility:SetControl("HistoryDataEditDialog_cancelButton")
        HistoryDataEditDialog_FocusTransitionUtility:SetControl("HistoryDataEditDialog_okButton")
    End Method

    Method ShowDateBox(value, pattern, separator)
    Begin
        With BtInputDateBoxCommon
            :SetOutputPattern(HistoryDataEditDialog_inputDateBox:Properties, pattern)
            :SetOutputSeparator(HistoryDataEditDialog_inputDateBox:Properties, separator)
            :SetText(HistoryDataEditDialog_inputDateBox:Properties, value)
        End With
    End Method

    Method ShowTextBox(value, inputControl, decimalPlaces)
    Begin
        Select Case inputControl
            Case INPUT_CONTROL_STRING_TEXT_BOX
                BtTextBoxCommon:SetInputMode(HistoryDataEditDialog_textBox:Properties, INPUT_MODE_SCAN_AND_KEY_ALPHANUMERIC)
            Case INPUT_CONTROL_INTEGER_TEXT_BOX
                BtTextBoxCommon:SetInputMode(HistoryDataEditDialog_textBox:Properties, INPUT_MODE_KEY_NUMERIC)
                BtTextBoxCommon:SetDecimalPlaces(HistoryDataEditDialog_textBox:Properties, decimalPlaces)
            Case INPUT_CONTROL_REAL_TEXT_BOX
                BtTextBoxCommon:SetInputMode(HistoryDataEditDialog_textBox:Properties, INPUT_MODE_KEY_NUMERIC)
                BtTextBoxCommon:SetDecimalPlaces(HistoryDataEditDialog_textBox:Properties, decimalPlaces)
            Case INPUT_CONTROL_RADIO_BUTTON
                BtTextBoxCommon:SetInputMode(HistoryDataEditDialog_textBox:Properties, INPUT_MODE_KEY_NUMERIC)
                BtTextBoxCommon:SetMinimum(HistoryDataEditDialog_textBox:Properties, 1)
                BtTextBoxCommon:SetMaximum(HistoryDataEditDialog_textBox:Properties, 8)
                BtTextBoxCommon:SetDecimalPlaces(HistoryDataEditDialog_textBox:Properties, 0)
            Case INPUT_CONTROL_CHECK_BOX
                BtTextBoxCommon:SetInputMode(HistoryDataEditDialog_textBox:Properties, INPUT_MODE_KEY_NUMERIC)
                BtTextBoxCommon:SetMinimum(HistoryDataEditDialog_textBox:Properties, 0)
                BtTextBoxCommon:SetMaximum(HistoryDataEditDialog_textBox:Properties, 1)
                BtTextBoxCommon:SetDecimalPlaces(HistoryDataEditDialog_textBox:Properties, 0)
        End Select
        BtTextBoxCommon:SetInitialText(HistoryDataEditDialog_textBox:Properties, value)
    End Method

    Method GetNewValue()
    Begin
        Return (_editValue)
    End Method

    Method KeyActionCancel(eventArgs)
    Begin
        BtButtonCommon:Focus(HistoryDataEditDialog_cancelButton:Properties)
        HistoryDataEditDialog_cancelButton:PerformClick()
    End Method

    Method KeyActionOk(eventArgs)
    Begin
        BtButtonCommon:Focus(HistoryDataEditDialog_okButton:Properties)
        HistoryDataEditDialog_okButton:PerformClick()
    End Method

    Method OnShown(e)
    Begin
        Select Case _logEditType
            Case LOG_EDIT_TYPE_DATE_BOX
                BtInputDateBoxCommon:SetVisible(HistoryDataEditDialog_inputDateBox:Properties, true)
                BtTextBoxCommon:SetVisible(HistoryDataEditDialog_textBox:Properties, false)
            Case LOG_EDIT_TYPE_TEXT_BOX
                BtInputDateBoxCommon:SetVisible(HistoryDataEditDialog_inputDateBox:Properties, false)
                BtTextBoxCommon:SetVisible(HistoryDataEditDialog_textBox:Properties, true)
        End Select

        HistoryDataEditDialog_res:Show(true)

        Select Case _logEditType
            Case LOG_EDIT_TYPE_DATE_BOX
                BtInputDateBoxCommon:Focus(HistoryDataEditDialog_inputDateBox:Properties)
            Case LOG_EDIT_TYPE_TEXT_BOX
                BtTextBoxCommon:Focus(HistoryDataEditDialog_textBox:Properties)
        End Select

        Window<"ROOT_WINDOW">:Update()

        HistoryDataEditDialog_res:EventWait()
    End Method



    Method LogEditDialog_KeyDown(sender)
    Begin
        HistoryDataEditDialog_res:KeyAssignedAction(sender)
    End Method

    Method OKButton_Click(sender, result)
    Begin
        Select Case _logEditType
            Case LOG_EDIT_TYPE_DATE_BOX
                _editValue = BtInputDateBoxCommon:GetText(HistoryDataEditDialog_inputDateBox:Properties)
            Case LOG_EDIT_TYPE_TEXT_BOX
                _editValue = BtTextBoxCommon:GetText(HistoryDataEditDialog_textBox:Properties)
        End Select

        DialogResult = DIALOG_RESULT_OK
        HistoryDataEditDialog_res:Close()
        HistoryDataEditDialog_res:Dispose()

        Event:Exit()
    End Method

    Method CancelButton_Click(sender, result)
    Begin
        DialogResult = DIALOG_RESULT_CANCEL
        HistoryDataEditDialog_res:Close()
        HistoryDataEditDialog_res:Dispose()

        Event:Exit()
    End Method

    Method OnPaint(e)
    Begin
    End Method

End Package

#popdefine
